JinjaTemplate:
  Template           <- (Text / Comment / RawStatement / Interpolation / SetStatement)+
  Text               <- ~(!(OpenInterpolation / OpenStatement / OpenComment) .)+
  Comment            <- OpenComment (!CloseComment .)+ CloseComment
  RawStatement       <- OpenRaw RawText CloseRaw
  SetStatement       <  OpenStatement "set" Variable "=" Expression Filter* CloseStatement
  Variable           <  qualifiedIdentifier
  RawText            <~ blank* (!CloseRaw .)+ blank*
  OpenRaw            <  OpenStatement "raw" CloseStatement
  CloseRaw           <  OpenStatement "endraw" CloseStatement
  OpenInterpolation  <- "{{"
  CloseInterpolation <- "}}"
  OpenStatement      <- "{%"
  CloseStatement     <- "%}"
  OpenComment        <- "{#"
  CloseComment       <- "#}"
  OpenParen          <- "("
  CloseParen         <- ")"
  Interpolation      <  OpenInterpolation Expression Filter* CloseInterpolation
  Expression         <~ qualifiedIdentifier / Value
  Number             <  ~(Digit+)
  Digit              <  [0-9]
  Filter             <  FilterStart FilterName FilterArgs*
  FilterName         <  identifier
  FilterStart        <  "|"
  FilterArgSep       <  ","
  FilterArg          <  String / Number / identifier / (!FilterArgSep !CloseParen !blank .)+
  FilterArgs         <  OpenParen FilterArg (FilterArgSep FilterArg)* CloseParen
  Dict               <  '{' (Pair (',' Pair)*)? '}'
  Pair               <  String ':' Value
  Array              <  '[' (Value (',' Value)* )? ']'
  Value              <  String
                        / Number
                        / Dict
                        / Array
                        / True
                        / False
                        / Null
  True               <- "true"
  False              <- "false"
  Null               <- "null"
